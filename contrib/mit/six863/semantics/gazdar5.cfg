%start Start
%kimmo "gazdar.kimmo.yaml"

Q -> DO[agr=?a] NP[-wh, agr=?a] AUX[+fin]/DO XP
Q -> DO[agr=?a] NP[-wh, agr=?a] AUX[+fin]/DO
Q -> BE[agr=?a] NP[-wh, agr=?a] AUX[+fin]/BE XP
Q -> BE[agr=?a] NP[-wh, agr=?a] AUX[+fin]/BE
Q -> HAVE[agr=?a] NP[-wh, agr=?a] AUX[+fin]/HAVE
Q -> HAVE[agr=?a] NP[-wh, agr=?a] AUX[+fin]
Q -> MODAL[+fin, agr=?a] NP[-wh, agr=?a] AUX[-fin]/MODAL[+fin] XP
Q -> MODAL[+fin, agr=?a] NP[-wh, agr=?a] AUX[-fin]/MODAL[+fin]
# modification: XP covers everything
XP/?x -> PP/?x | AP/?x | NP/?x | VP[-fin]/?x
Q-W/?x -> DO[agr=?a] NP[-wh, agr=?a] AUX[+fin]/DO XP/?x
Q-W/?x -> BE[agr=?a] NP[-wh, agr=?a] AUX[+fin]/BE XP/?x
Q-W/?x -> HAVE[agr=?a] NP[-wh, agr=?a] AUX[+fin]/HAVE XP/?x
Q-W/?x -> MODAL[+fin, agr=?a] NP[-wh, agr=?a] AUX[-fin]/MODAL[+fin] XP/?x

[pos=?p, +empty, wh=?w]/[pos=?p, wh=?w] ->

Q -> ADVP[+wh] Q-W/ADVP[+wh]
Q -> AP[+wh] Q-W/AP[+wh]
Q -> PP[+wh] Q-W/PP[+wh]
Q -> NP[+wh] Q-W/NP[+wh]
QBAR -> COMP[+wh] S[+fin]
QBAR -> ADVP[+wh] S/ADVP[+wh]
# to be fixed: make agreement work here
QBAR -> AP[+wh] S/AP[+wh]
QBAR -> PP[+wh] S/PP[+wh]
QBAR -> NP[+wh] S/NP[+wh]
QBAR -> NP[+wh] S-W/NP[+wh]
Start -> Q | QBAR | NP | S[+fin]
R[+wh] -> PP[+wh] S[+fin]/PP[+wh]
R[wh=?w] -> S[+fin]/NP[wh=?w]
R[wh=?w] -> RCOMP S[+fin]/NP[wh=?w]
R[wh=?w] -> RCOMP S-W[+fin]/NP[wh=?w]
NBAR[-wh] -> FACT SBAR
NP[wh=?w, agr=?a] -> NP[wh=?w, agr=?a] R[wh=?w]

VP[fin=?f, agr=?a]/?x -> V[class=18, fin=?f, agr=?a] *FOR S[-fin]/?x
VP[fin=?f, agr=?a]/?x -> V[class=17, fin=?f, agr=?a] NP/?x PP PP
VP[fin=?f, agr=?a]/?x -> V[class=17, fin=?f, agr=?a] NP PP PP/?x
VP[fin=?f, agr=?a]/?x -> V[class=16, fin=?f, agr=?a] NP/?x AP
VP[fin=?f, agr=?a]/?x -> V[class=16, fin=?f, agr=?a] NP AP/?x
VP[fin=?f, agr=?a]/?x -> V[class=15, fin=?f, agr=?a] VBAR[-fin]/?x
VP[fin=?f, agr=?a]/?x -> V[class=14, fin=?f, agr=?a] S[-fin]/?x
VP[fin=?f, agr=?a]/?x -> V[class=13, fin=?f, agr=?a] NP/?x SBAR
VP[fin=?f, agr=?a]/?x -> V[class=13, fin=?f, agr=?a] NP SBAR/?x
VP[fin=?f, agr=?a]/?x -> V[class=12, fin=?f, agr=?a] PP/?x QBAR
VP[fin=?f, agr=?a]/?x -> V[class=12, fin=?f, agr=?a] PP QBAR/?x
VP[fin=?f, agr=?a]/?x -> V[class=11, fin=?f, agr=?a] NP/?x QBAR
VP[fin=?f, agr=?a]/?x -> V[class=11, fin=?f, agr=?a] NP QBAR/?x
VP[fin=?f, agr=?a]/?x -> V[class=10, fin=?f, agr=?a] QBAR/?x
VP[fin=?f, agr=?a]/?x -> V[class=9, fin=?f, agr=?a] S/?x
VP[fin=?f, agr=?a]/?x -> V[class=8, fin=?f, agr=?a] SBAR/?x
VP[fin=?f, agr=?a]/?x -> V[class=7, fin=?f, agr=?a] NP/?x NP
VP[fin=?f, agr=?a]/?x -> V[class=7, fin=?f, agr=?a] NP NP/?x
VP[fin=?f, agr=?a]/?x -> V[class=A, fin=?f, agr=?a] NP/?x PP | V[class=B, fin=?f, agr=?a] NP/?x PP | V[class=6, fin=?f, agr=?a] NP/?x PP
VP[fin=?f, agr=?a]/?x -> V[class=A, fin=?f, agr=?a] NP PP/?x | V[class=B, fin=?f, agr=?a] NP PP/?x | V[class=6, fin=?f, agr=?a] NP PP/?x
VP[fin=?f, agr=?a]/?x -> V[class=C, fin=?f, agr=?a] PP/?x
VP[fin=?f, agr=?a]/?x -> V[class=A, fin=?f, agr=?a] ADVP | V[class=C, fin=?f, agr=?a] ADVP/?x
VP[fin=?f, agr=?a]/?x -> V[class=3, fin=?f, agr=?a] AP/?x
VP[fin=?f, agr=?a]/?x -> V[class=A, fin=?f, agr=?a] NP/?x | V[class=B, fin=?f, agr=?a] NP/?x
VP[fin=?f, agr=?a] -> V[class=A, fin=?f, agr=?a] | V[class=C, fin=?f, agr=?a]

VBAR[fin=?f] -> AUX[fin=?f] VP[-fin]
VBAR[+fin] -> VP[+fin]
VBAR[fin=?f]/?x -> AUX[fin=?f] VP[-fin]/?x
VBAR[+fin]/?x -> VP[+fin]/?x

AUXADVP -> *NOT ADVP[-wh] *NOT | *NOT ADVP[-wh] | ADVP[-wh] *NOT | ADVP[-wh] | *NOT
DOP[agr=?a]/?x -> DO[agr=?a]/?x
BEP[agr=?a]/?x -> BE[agr=?a]/?x
HAVEP[agr=?a]/?x -> HAVE[agr=?a]/?x
MODALP[agr=?a, fin=?f]/?x -> MODAL[agr=?a, fin=?f]/?x
DOP[agr=?a]/?x -> DO[agr=?a]/?x AUXADVP
BEP[agr=?a]/?x -> BE[agr=?a]/?x AUXADVP
HAVEP[agr=?a]/?x -> HAVE[agr=?a]/?x AUXADVP
MODALP[fin=?f, agr=?a]/?x -> MODAL[fin=?f, agr=?a]/?x AUXADVP
AUX[fin=?f, agr=?a]/?x -> AUXADVP MODALP[fin=?f, agr=?a]/?x HAVEP BEP
AUX[fin=?f, agr=?a]/?x -> AUXADVP MODALP[fin=?f, agr=?a]/?x BEP
AUX[fin=?f, agr=?a]/?x -> AUXADVP MODALP[fin=?f, agr=?a]/?x HAVEP
AUX[+fin, agr=?a]/?x -> AUXADVP DOP[+fin, agr=?a]/?x
AUX[+fin, agr=?a]/?x -> AUXADVP HAVEP[+fin, agr=?a]/?x BEP
AUX[+fin, agr=?a]/?x -> AUXADVP HAVEP[+fin, agr=?a]/?x
AUX[+fin, agr=?a]/?x -> AUXADVP BEP[+fin, agr=?a]/?x
AUX[fin=?f, agr=?a]/?x -> AUXADVP MODALP[fin=?f, agr=?a]/?x
AUX[+fin] -> AUXADVP
AUX[fin=?f, agr=?a]/?x -> MODALP[fin=?f, agr=?a]/?x HAVEP BEP
AUX[fin=?f, agr=?a]/?x -> MODALP[fin=?f, agr=?a]/?x BEP
AUX[fin=?f, agr=?a]/?x -> MODALP[fin=?f, agr=?a]/?x HAVEP
AUX[+fin, agr=?a]/?x -> DOP[agr=?a]/?x
AUX[+fin, agr=?a]/?x -> HAVEP[agr=?a]/?x BEP
AUX[+fin, agr=?a]/?x -> BEP[agr=?a]/?x
AUX[+fin, agr=?a]/?x -> HAVEP[agr=?a]/?x
AUX[fin=?f, agr=?a]/?x -> MODALP[fin=?f, agr=?a]/?x
NBAR[-wh, agr=?a] -> N[-wh, agr=?a] | AP NBAR[-wh, agr=?a] | NBAR[-wh, agr=?a] PP
NP[-wh, agr=?a] -> A[+compar] NBAR[-wh, agr=?a] *THAN S[+fin]
NP[-wh, agr=?a] -> A[+compar] NBAR[-wh, agr=?a] *THAN S[+fin]/NBAR
NP[wh=?w, agr=?a] -> DET[wh=?w] NBAR[-wh, agr=?a] | PRO[wh=?w, agr=?a]
NP[-wh, agr=?a] -> NAME[agr=?a]
PP[wh=?w]/?x -> P NP[wh=?w]/?x
PP[-wh] -> *PP
ADVP[+wh] -> SPEC[+wh] ADVP[-wh] | *ADVP[+wh]
ADVP[-wh] -> ADVP[-wh] ADV | ADV
AP[wh=?a] -> AP[wh=?a] VBAR[-fin]/NP[-wh] | ADVP[wh=?a] A[-compar]
AP[+wh] -> SPEC[+wh] AP[-wh]
AP[-wh] -> AP[-wh] A[-compar] | A[-compar] | N
SBAR/?x -> COMP[-wh] S[+fin]/?x
S[+fin] -> QBAR AUX[+fin] VP[+fin]/QBAR | QBAR VP[+fin]/QBAR
S[+fin] -> SBAR AUX[+fin] VP[+fin]/SBAR | SBAR VP[+fin]/SBAR
S[fin=?f] -> NP[-wh, agr=?a] AUX[fin=?f, agr=?a] XP
## The equivalent of this rule is commented out in the original:
S[fin=?f]/?x -> NP[-wh, agr=?a] AUX[fin=?f, agr=?a] XP/?x
S[fin=?f]/?x -> NP[-wh, agr=?a]/?x AUX[fin=?f, agr=?a] XP
S[fin=?f] -> NP[-wh, agr=?a] AUX[fin=?f, agr=?a]
S[fin=?f]/?x -> NP[-wh, agr=?a]/?x AUX[fin=?f, agr=?a] VP[fin=?f]
S[fin=?f]/?x -> NP[-wh, agr=?a] AUX[fin=?f, agr=?a] VP[fin=?f]/?x
S[+fin]/?x -> NP[-wh, agr=?a]/?x VP[+fin, agr=?a]
S[+fin]/?x -> NP[-wh, agr=?a] VP[+fin, agr=?a]/?x
## Agreement?
S-W[fin=?f]/NP -> AUX[fin=?f] VP[fin=?a]
S-W[+fin]/NP -> VP[+fin]

VBAR[fin=?f]/?x -> VBAR[fin=?f]/?x CONJ VBAR[fin=?f]/?x
AP[wh=?w]/?x -> AP[wh=?w]/?x CONJ AP[wh=?w]/?x
NP[wh=?w]/?x -> NP[wh=?w]/?x CONJ NP[wh=?w]/?x
VP[fin=?f, agr=?a]/?x -> VP[fin=?f, agr=?a]/?x CONJ VP[fin=?f, agr=?a]/?x
S[fin=?f]/?x -> S[fin=?f]/?x CONJ S[fin=?f]/?x

# Rightward extraposition
S[fin=?f] -> S[fin=?f]/NP[wh=?w] NP[wh=?w]
S[fin=?f] -> S[fin=?f]/PP[wh=?w] PP[wh=?w]
S[fin=?f] -> S[fin=?f]/AP[wh=?w] AP[wh=?w]
S[fin=?f] -> S[fin=?f]/ADVP[wh=?w] ADVP[wh=?w]
S[fin=?f] -> S[fin=?f]/R[wh=?w] R[wh=?w]
S[fin=?f] -> S[fin=?f]/Q Q
S[fin=?f] -> S[fin=?f]/QBAR QBAR
S[fin=?f] -> S[fin=?f]/SBAR SBAR

R[wh=?v] -> R[wh=?v]/NP[wh=?w] NP[wh=?w]
R[wh=?v] -> R[wh=?v]/PP[wh=?w] PP[wh=?w]
R[wh=?v] -> R[wh=?v]/AP[wh=?w] AP[wh=?w]
R[wh=?v] -> R[wh=?v]/ADVP[wh=?w] ADVP[wh=?w]
R[wh=?v] -> R[wh=?v]/S[fin=?f] S[fin=?f]
R[wh=?v] -> R[wh=?v]/Q Q
R[wh=?v] -> R[wh=?v]/QBAR QBAR
R[wh=?v] -> R[wh=?v]/SBAR SBAR

Q -> Q/NP[wh=?w] NP[wh=?w]
Q -> Q/PP[wh=?w] PP[wh=?w]
Q -> Q/AP[wh=?w] AP[wh=?w]
Q -> Q/ADVP[wh=?w] ADVP[wh=?w]
Q -> Q/R[wh=?w] R[wh=?w]
Q -> Q/S[fin=?f] S[fin=?f]
Q -> Q/QBAR QBAR
Q -> Q/SBAR SBAR

QBAR -> QBAR/NP[wh=?w] NP[wh=?w]
QBAR -> QBAR/PP[wh=?w] PP[wh=?w]
QBAR -> QBAR/AP[wh=?w] AP[wh=?w]
QBAR -> QBAR/ADVP[wh=?w] ADVP[wh=?w]
QBAR -> QBAR/R[wh=?w] R[wh=?w]
QBAR -> QBAR/S[fin=?f] S[fin=?f]
QBAR -> QBAR/Q Q
QBAR -> QBAR/SBAR SBAR

SBAR -> SBAR/NP[wh=?w] NP[wh=?w]
SBAR -> SBAR/PP[wh=?w] PP[wh=?w]
SBAR -> SBAR/AP[wh=?w] AP[wh=?w]
SBAR -> SBAR/ADVP[wh=?w] ADVP[wh=?w]
SBAR -> SBAR/R[wh=?w] R[wh=?w]
SBAR -> SBAR/S[fin=?f] S[fin=?f]
SBAR -> SBAR/Q Q
SBAR -> SBAR/QBAR QBAR

